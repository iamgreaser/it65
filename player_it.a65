; IT65, the generic 6502 .it driver
; by Ben "GreaseMonkey" Russell, 2011. Public domain.
; The actual thing

.align $0100
.byte "it65/GM."

; trashes A
player_it_init
	; set speeds
	lda SRCTPR
	sta CURTPR
	lda SRCBPM
	sta CURBPM
	
	; set some other stuff
	lda #$00
	sta CURORD
	sta BRKROW
	sta ACCBPM
	
	lda #$01
	sta TICKREM
	
	jsr player_it_nextpat
	
	rts

; trashes A, Y (via func)
player_it_tick
	; add BPM to the accumulated BPM
	lda ACCBPM
	clc
	adc CURBPM
	sta ACCBPM
	
	; spaghetti code: jump to the check if the carry is clear.
	bcc +
	-
		; do a player tick
		jsr player_it_dotick
		
		; subtracterate
		lda ACCBPM
		clc
		sbc #ROOTBPM
		sta ACCBPM
	+
		; check: is ACCBPM >= ROOTBPM?
		lda ACCBPM
		cmp #ROOTBPM
		bcc - ; if so, jump back.
	rts

; trashes A, X (via func), Y (via func)
player_it_dotick
	; well yeah. as expected, this thing.
	inc CURTICK
	
	dec TICKREM
	bne ++
		; reset tick
		lda #$00
		sta CURTICK
		lda CURTPR
		sta TICKREM
		
		; check if there are any rows left; if not, fetch another pattern
		lda ROWREM
		cmp #$00
		bne +
			; fetch!
			jsr player_it_nextpat
		+
		
		; load some stuff
		jsr player_it_getrow
		jsr player_it_parserow_0
		jmp ++
	+
		jsr player_it_parserow_n
	+
	rts

; trashes A, X
player_it_nextpat
	; load order into X for now
	lda CURORD
	inc CURORD
	asl
	tax
	
	; load pattern into order list
	lda ORDLIST,x
	sta BEGPAT
	sta CURPAT
	lda ORDLIST+1,x
	sta BEGPAT+1
	sta CURPAT+1
	
	; check if 0 -- SEPARATOR
	cmp #$00
	beq player_it_nextpat
	
	; check if 1 -- END
	cmp #$01
	bne +
		lda #$00
		sta CURORD
		jmp player_it_nextpat
	+
	
	; get the row count
	jsr player_it_getbyte
	
	; skip a bunch of rows if necessary
	; TODO!!! check if this would go past the last row!!!
	inc BRKROW
	dec BRKROW
	beq +
	-
		jsr player_it_getrow
		dec BRKROW
		bne -
	+
	
	rts

; trashes Y
; increments word at CURPAT
; returns in A
player_it_getbyte
	ldy #$00
	lda (CURPAT),y
	inc CURPAT
	bne +
		inc CURPAT+1
	+
	rts

; trashes A, X, Y (via func)
player_it_getrow
	; ok, do the stuff you're good at
	
	; clear the pattern touch thing
	ldx #$20-2
	lda #$00
	-
		sta PATTCH,x
		dex
		dex
		bpl -
	
	; load byte
	jsr player_it_getbyte
	
	; is it the end of the row?
	cmp #$00
	bne +
		; yep
		dec ROWREM
		rts
	+
	
	; subtract 1, shift left, lop off top bit and check
	clc
	sbc #$01
	asl
	tax
	bcc +
		; bit is set - load new mask
		jsr player_it_getbyte
		sta PATMASK,x
	+
	
	; inform the driver that this channel has been touched
	inc PATTCH,x
	
	; now deal to mask
	lda PATMASK,x
	sta T_GEN1
	sta T_GEN2
	
	; keep effect?
	asl T_GEN2
	bcs +
		; nope
		lda #$00
		sta PATEFT,x
		sta PATEFP,x
	+
	
	; keep volume?
	asl T_GEN2
	bcs +
		; nope
		lda #$FF
		sta PATVOL,x
	+
	; keep instrument?
	asl T_GEN2
	bcs +
		; nope
		lda #$00
		sta PATINS,x
	+
	; keep note?
	asl T_GEN2
	bcs +
		; nope
		lda #253
		sta PATNOTE,x
	+
	
	; now shift in the other dir
	; new note?
	lsr T_GEN1
	bcc +
		; yep
		jsr player_it_getbyte
		sta PATNOTE,x
	+

	; new instrument?
	lsr T_GEN1
	bcc +
		; yep
		jsr player_it_getbyte
		sta PATINS,x
	+

	; new volume?
	lsr T_GEN1
	bcc +
		; yep
		jsr player_it_getbyte
		sta PATVOL,x
	+

	; new effect?
	lsr T_GEN1
	bcc +
		; yep
		jsr player_it_getbyte
		sta PATEFT,x
		jsr player_it_getbyte
		sta PATEFP,x
	+
	
	; now loop again!
	jmp player_it_getrow

; trashes A, X
player_it_parserow_0
	ldx #$00
	-
		; TODO lots of stuff
		; this will eventually be replaced with something accurate
		
		; if not touched, skip everything
		lda PATTCH,x
		cmp #$00
		beq js_player_it_parserow_0
		
		; load instrument
		
		; --- EFFECTS ---
		; ! TODO ! Will use a LUT once this is eventually touched on
		
		; --- INSTRUMENTS ---
		lda PATINS,x
		cmp #$00 ; can't have a zero instrument number
		beq +
			jsr callback_doins
		+
		
		; --- VOLUMES ---
		lda PATVOL,x
		cmp #65
		bcc +
			; ok it's actually a volume
			jsr callback_dovol
			jmp js_player_it_parserow_vol
		+
		
		; TODO: voleffects! (and maaaaaaybe pan)
		
		js_player_it_parserow_vol
		
		; --- NOTES ---
		
		lda PATNOTE,x
		
		; actual note?
		cmp #120
		bcc +
			; yes
			sta CHNOTE+1,x
			lda #$00
			sta CHNOTE,x
			
			jsr callback_donote
			
			jmp js_player_it_parserow_note
		+
		; note cut?
		cmp #254
		bne +
			; yes
			jsr callback_notecut
			
			jmp js_player_it_parserow_note
		+
		; either note off (?TODO?) or note fade (?TODO?) or nothing
		js_player_it_parserow_note
		
		js_player_it_parserow_0
		
		; and loop
		
		inx
		inx
		cpx #$20
		bne -
	
	rts

; will eventually trash A, X
player_it_parserow_n
	; TODO effects 'r' us
	rts
